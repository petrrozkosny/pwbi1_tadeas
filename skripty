01 suma srazek = 

-- scenar1: Respektuji filtry vizualu, zadne nerusim, nepridavam. Pouzivam zakladni agregacni funkce
-- typu SUM(),MIN()
SUM(data[srazky])

__________________________

02 suma srazek ruzyne = 
-- suma srazek v danem roce v lokalite ruzyne

-- scenar2: Respektuji filtry vizualu a nastavuji dalsi, tj. pouziji kombinaci funkci
-- SUMX(FILTER()), MINX(FILTER()), MAXX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data se vsemi jejimi filtry
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
        data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
    data[srazky])
    
    ___________________________________
    
    03 suma razek vsechny roky = 

-- scenar3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji kombinaci funkci
-- SUMX(ALL()),MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data se vsemi jejimi hodnotami,
    -- tj. tabulka neni filtrovana
    ALL(data),
--krok2: Funkce SUMX() scita sloupec srazky tabulky vracene funkci ALL()
data[srazky])

_________________________________________________


04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MINX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    -- krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec rok tabulky vracene funkci ALL()
    data[rok] = 2000),
--krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
data[srazky])

_______________________________


05 suma srazek mezirocne = 
SUMX(
FILTER(
    ALL(data),
data[rok] = SELECTEDVALUE(data[rok]) - 1
&& data[lokalita] = SELECTEDVALUE(data[lokalita]))
,data[srazky])

___________________________________________________________

06 maximalni srazky = MAX(data[srazky])

___________________________________________________

07 maximalni srazky = 

if(
    -- maximalni srazky v roce a lokalite
    MAX(data[srazky])
    =
    MAXX(
        FILTER(
            ALL(data),
        data[lokalita] = SELECTEDVALUE(data[lokalita])),
    data[srazky]),
    "red")

______________________________________

08 prumerne rocni srazky = 
AVERAGEX(
    ALL(data[rok]),
    CALCULATE(SUM(data[srazky])))

______________________________________________

09 podil srazek ruzyne = 

-- suma srazek v danem roce v lokalite ruzyne
SUMX(
    FILTER(data,
    data[lokalita] = "RUZYNE"),
data[srazky])
-- deleno
/
-- suma srazek v danem roce 
SUM(data[srazky])

________________________________________________________

10 suma srazek ruzyne mesic 7 = 
-- suma srazek v danem roce, lokalita ruzyne, mesic 7

SUMX(
    FILTER(data,
    data[lokalita]  = "RUZYNE"
    && data[mesic] = 7),
data[srazky])

______________________________________________________________

11 suma srazek mezirocne = 

-- suma srazek v danem roce
SUM(data[srazky])
-- minus
-
-- suma srazek v predchozim roce
SUMX(
    FILTER(
        ALL(data),
    data[rok] = SELECTEDVALUE(data[rok]) - 1),
data[srazky])

________________________________________________________________________________

12 maximalni srazky v ruzyne v danem roce = 
--krok4: Funkce MAXX() hleda maximum ve sloupci srazky
-- tabulky vracene funkci FILTER()
MAXX(
--krok3: Funkce FILTER() vraci tabulku
FILTER(
-- krok1: Funkce FILTER() vytvari kopii tabulky data,
-- respektuje filtry vizualu
data,
--krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
data[lokalita] = "RUZYNE"),
data[srazky])

_________________________________________________________________________

13 suma srazek kumulativne = 
SUMX(
    FILTER(ALL(data),
    data[rok] <= SELECTEDVALUE(data[rok])),
data[srazky])


____________________________________________________


14 den maximalni teploty = 

-- prvni (minimalni) datum maximalni teploty

-- minimalni hodnota sloupce datum z tabulky vracene funkci filter(),

MINX(
    FILTER(
    -- kopie tabulky data, respektuji filtr roku
    data,
    -- nastavuji filtr na sloupec maximalni_teplota
    data[maximalni_teplota] = 
    -- maximu z maximalni teploty v danem roce
    MAX(data[maximalni_teplota])),
data[datum])

___________________________________________________

15 pocet destivych dnu ruzyne = 
-- v kolika datumech v danem roce v lokalite ruzyne 
-- byly srazky >0

COUNTX(
    FILTER(data,
    data[lokalita] = "RUZYNE"
    && data[srazky] >0),
data[datum])

________________________________________

16 byla ruzyne nejteplejsi lokalita = 

-- ano / ne
IF(
-- maximalni teplota v danem roce v ruzyne
MAXX(
    FILTER(data,
    data[lokalita] = "RUZYNE"),
data[maximalni_teplota])
=
-- maximalni teplota v danem roce, vsechny lokality
MAX(data[maximalni_teplota]),"ANO","NE")
________________________________________

17 pocet tropickych dnu ruzyne = 
-- maximalni_teplota >=30
COUNTX(
    FILTER(data,
    data[lokalita] = "RUZYNE"
    && data[maximalni_teplota] >=30),
data[datum])

________________________________________________________

18 pocet tropickych dnu = 
-- pocet datumu kdy maximalni_teplota >=30

COUNTX(
    FILTER(
    data,
    data[maximalni_teplota] >=30),
data[datum])

_________________________________________

19 suma srazek kosice = 

SUMX(
    FILTER(data,
    data[lokalita] = "KOSICE"),
data[srazky])
